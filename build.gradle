plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'java'
    id 'base'
}

group = 'de.stamme'
version = '1.0'

base {
    archivesName = "ChestRestack"
}



repositories {
    mavenCentral()
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://oss.sonatype.org/content/groups/public/' }
    maven { url = 'https://repo.minebench.de/' }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.20.4-R0.1-SNAPSHOT"
    compileOnly 'org.jetbrains:annotations:24.0.1'

    implementation 'de.themoep:minedown:1.7.0-SNAPSHOT'
    implementation 'org.bstats:bstats-bukkit:3.0.0'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

shadowJar {
    archiveClassifier.set(null)
    destinationDirectory = file(devServerPathPaper20_4)

    relocate 'org.bstats', 'de.stamme.chestrestack.lib.bstats'
    relocate 'de.themoep.minedown', 'de.stamme.chestrestack.lib.minedown'
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

build.dependsOn shadowJar
